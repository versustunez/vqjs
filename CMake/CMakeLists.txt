macro(add_sources)
    get_property(tmp GLOBAL PROPERTY SRCS)
    file(RELATIVE_PATH _relPath "${PROJECT_SOURCE_DIR}" "${CMAKE_CURRENT_SOURCE_DIR}")
    foreach (_src ${ARGN})
        if (_relPath)
            list(APPEND tmp "${_relPath}/${_src}")
        else ()
            list(APPEND tmp "${_src}")
        endif ()
    endforeach ()
    set_property(GLOBAL PROPERTY SRCS ${tmp})
endmacro()

macro(set_alternate_linker linker)
    find_program(LINKER_EXECUTABLE ld.${USE_ALTERNATE_LINKER} ${USE_ALTERNATE_LINKER})
    if(LINKER_EXECUTABLE)
        if("${CMAKE_CXX_COMPILER_ID}" STREQUAL "Clang" AND "${CMAKE_CXX_COMPILER_VERSION}" VERSION_LESS 12.0.0)
            add_link_options("-ld-path=${USE_ALTERNATE_LINKER}")
        else()
            add_link_options("-fuse-ld=${USE_ALTERNATE_LINKER}")
        endif()
    else()
        set(USE_ALTERNATE_LINKER "" CACHE STRING "Use alternate linker" FORCE)
    endif()
endmacro()

set(USE_ALTERNATE_LINKER "" CACHE STRING "Use alternate linker. Leave empty for system default; alternatives are 'gold', 'lld', 'bfd', 'mold'")
if(NOT "${USE_ALTERNATE_LINKER}" STREQUAL "")
    set_alternate_linker(${USE_ALTERNATE_LINKER})
endif()
